# Переключение режима новеллы
define menu = nvl_menu
define narrator = nvl_narrator


# Определение персонажей игры.
define mc = Character('Василиса', image='vasilisa', color='#38977c', kind=nvl, what_prefix = '   {cps=95}"', what_suffix='"')
define koschey = Character('Кощей Бессмертный', color='#ED8F33', kind=nvl, what_prefix = '   {cps=95}"', what_suffix='"')
define yaga = Character('Баба Яга', color='#b03636', kind=nvl, what_prefix = '   {cps=80}"', what_suffix='"')
define likho = Character('Лихо Лесное', color='#5F990C', kind=nvl, what_prefix = '   {cps=120}"', what_suffix='"')
define snake = Character('Заколдованная', color='#9466BF', kind=nvl, what_prefix = '   {cps=65}"', what_suffix='"')


# Звуки
define background = '2020-02-22_-_Relaxing_Green_Nature_-_David_Fesliyan.mp3'


# Изображения персонажей
image side vasilisa = 'vasilisa_first.png'
image side vasilisa_second = 'vasilisa_second.png'


init:
    $ relation_current = 0
    $ relation_max = 100
    $ first_choice = None
    $ diastyle = 2


label transition(text, text_label=None, time=3.0, size=30, align=0.5, color=gui.selected_color):
    scene black with dissolve
    
    show screen start_transition(text, text_label, align, size, color) with dissolve

    pause time

    scene black with dissolve  

    return


label start:
    $ quick_menu = False
    play music background

    call transition(
        text='''"У любого супружества два сердца,
        одно светлое и одно тёмное."''', 
        text_label='Стивен Кинг, "История Лизи"', 
        align=0.0,
    )
    call transition(text='СОКРОВИЩЕ ТЁМНОГО СЕРДЦА', size=40, color=gui.accent_color)

    $ start_transition = False

    # TODO добавить описание в "Об игре" (Добавить шрифт и описание из обсуждения)
    
    scene background_image
    show frame

    $ quick_menu = True
    'В некотором царстве, в некотором государстве жила-была царевна, и имя ей было Василиса. Была она - умом одаренная, невиданной красоты да силы неслыханной. Правила она год, другой, и все лета царствования своего искала  себе мужа, - по силе равного да богатством не обделенного.' 
    
    'Приходили к ней богатыри ладные, царевичи славные, но никто Василисе не приглянулся. Надоело ей ждать, когда счастье её найдет, и отправилась она сама, да не куда-то, а на поиски Кощея Бессмертного. Слыхала она, что богатства у него немерено, сил невиданно, да жизнь надежно спрятана.'

    show forest behind frame

    'Находясь в лесной глуши, царевна направляется к той единственной, кто может помочь ей добраться в царство Кощея.'

    'День за днём, год за годом, оберегает она путь к царству мертвому, провожает души, да не позволяет живым перейти в мир иной.'

    'Путь к дому Бабы Яги темен и извилист, идет он через зыбкие топи, да густую чащу, полную кустов терновника. Но не тронута кожа Василисы иглами острыми, не сыры ноги её от воды мёртвой, словно и не шла она три ночи к дому ведьмы.'

    'И вот стоит царевна перед домом Яги, да держит в руках букет из полыни и чертополоха, и хоть неспокойна душа её, знает, - другого пути к Кощею нет.'

    mc 'Долгий путь, Яга, я проделала, шла к тебе три дня и три ночи, черной птицей обернулась, черным вороном, не видел меня ни зверь твой, да не ты сама, ведьма окаянная. Так повернись изба, ко мне передом, к лесу - задом!'

    'Повязала Василиса букет на пояс около верного меча своего, сняла сапоги, и босыми ногами обошла дом Яги три раза. Глаза в пол не опускала, смотрела на лесную чащу, да чувствовала, как ледяная трава и корни, словно ловкие пальцы, пытаются ухватить её за ноги.'

    'Стоит царевна перед порогом избы, и слышит, как идет хозяйка дома, разной поступью идёт, - одна нога ступает мягко, словно кошка крадется, а вторая, - со звоном, да холодным таким, как звон множества колокольчиков.'

    'Слышит девушка - двери Яга отпирает, и думается ей, вот увидит она сейчас ту, о которой с детства говорили - “стара и страшна, поганая баба, костяной ногой в том мире стоит, детей в полдень крадет, и в лютый мороз губит".'

    'Но стоит перед царевной не костлявая старуха, а женщина, чьей красоты мир не видел, - красивее всех царевн. Волосы черные как ночь, волнами ниспадают до самой земли, и только то тут, то там, - серебряные змейки вьются в них. Кожа её солнцем поцелована, глаза - зеленый малахит, а губы красны словно кровь.'

    yaga 'Чего тебе надобно, негодница? Смотри, выйду к тебе, царевна, костяной ногой, обратно не вернешься, так и останешься со мной.'

    mc 'Доброго вечера, Яга, пришла я к тебе из своего царства, по болотам зыбким шла, по тропам неведомым, да ради знания о том, как пройти в царство Кощеево.'

    mc 'Сильна я и умна, Яга, да нет в царстве моём дружины крепкой, казны полной, и главное - душа моя одинока. Не нашлось еще богатыря, что был бы равен мне и умом, и силой. Кощей только остался - последняя надежда моя. Коль приглянусь я ему, будет правление наше складным и ладным.'

    yaga 'А коль не приглянешься? Что тогда?'

    'Стоит Яга за порогом, да хитро на Василису смотрит, с ехидством, словно знает то, что сама царевна не знает'
    
    'И отвечает ей царевна, и холодно так отвечает, а в глазах её огонь блещет.'

    mc 'Коль не приглянусь? Не сносить ему головы, да лишится он всех своих сокровищ. Одна править стану'

    yaga 'Хочешь сказать, что ты, Василиса, сможешь Кощея одолеть?'

    yaga 'Ну так заходи в избу, уж больно любопытно мне, осмелишься ли ты Кощея с трона сместить, аль может и полюбит он тебя.'

    'И ступила Яга за порог живой ногой, приглашая царевну в избу. Теплом печи повеяло, травами лесными, с лугов собранных.'

    hide forest
    show hut behind frame

    'Заходит царевна в избу, через сени проходит, да без стеснения осматривается. Светло в избе, по центру печь расположена - украшена она узорами и письменами, а над нею полог кружевной висит. Дивится царица - травы невиданные, в пучки собранные, под самым потолком развешаны, а по стенам избы то тут, то там, черепа зверей висят, да глазницы их искрами света мерцают.'

    yaga 'Раз пришла ты ко мне, то, верно, знаешь, что от тебя попрошу я? Да вот только кажется мне, что просить тебя воды натаскать, да всякое на веретёнце по ниточке напрядсти, толку нет.'

    yaga 'Ты ответь-ка мне, Василиса, тот перстень, что ты на руке левой носишь, отдашь ли его? Помнится мне, что перстень то этот не простой - путь домой он укажет, да ложь чужую покажет.'

    yaga 'Вот если готова ты с ним расстаться, то помогу тебе дорогу найти в Кощеево царство.'

    'Услышала Василиса просьбу Яги, да побледнела, и не от того, что перстень волшебный был, а потому что единственной вещью он остался в память о сестре её, Марье, что была отдана в жены змею. И ведь знала же Яга об этом, и всё равно в дар перстень запросила.'

    'И так на Ягу глянула царевна, с обидою страшною, да сняла с руки перстень. Был подарен он Марье отцом с матерью в день её свадьбы, прежде чем забрал сестру змей из отчего дома.'

    'Год спустя Василиса и получила этот перстень, в напоминание о жизни сестры,  короткой, да славной.'

    mc 'Уколоть меня решила, Яга? Но так и быть, забирай.'

    yaga 'Больно легко ты с ним расстаешься, Василиса, не горюешь по сестре, аль не печалишься о том, что убила змея? Ведь не повинен он был в смерти сестры твоей.'

    yaga 'Если ты вернуть её намерена, то знай, что провожала я Марью в царство Кощеева, но обратно она не воротится. Да и нет её там уже давно, душа её светлее других была. Не с Кощеем она больше.'

    'Но не за сестрой отправляется царевна, а за богатствами, да судьбу испытать. Помнила она, как сладко жили Змей и Марья, и без зависти была счастлива за них, да мечтала, что однажды и ей, с её холодным сердцем, тяжелой рукой да острым умом, найдется суженный.'

    mc 'Змей по своей вине мертв, - не уберег сестру мою, ту, которую так любил, которой о верности клялся. Милосердно с ним я поступила, а иначе бы сидеть ему в темнице до скончания времен.'

    mc 'А что не вернуть мне её, - сама знаю. Не за этим я прошу тебя о помощи.'

    yaga 'Много девок к Кощею просятся, да никто еще не возвращался, всех он в ящерок да птичек превращает. Но верится мне, но коль даже если не полюбит он тебя, иль сокровища ты его не получишь, встретимся мы вновь.'

    yaga 'И коль вернешься ко мне, есть одно предложение к тебе, царевна.'

    'И наклонилась она к царевне, да прошептала ей на ухо заветное предложение.'

    'В ответ кивнула девушка, давая свое безмолвное согласие.'

    yaga 'А теперь не стой столбом, Василиса - владычица царства вороньевых да змеиных, встань у печи, да слушай внимательно.'

    yaga 'Покуда жива ты, не могу я просто так пустить тебя в царство Кощея, должна ты сама открыть себе путь.'

    yaga 'Не вернуться тебе в мир живых, коль не готова ты пожертвовать миру мертвых себя. Всё, что угодно отдай, но с условием одним - ту часть твою, что едина с тобою.'

    'И топнула Яга ногой костлявой, да зазвенели колокольчики, что украшали ту самую ногу. Вспыхнул огонь в печи, ухнула птица где-то в лесной глуши, еще ярче загорелись искры в глазницах черепов зверей, да так ярко, словно полдень летний в самом разгаре.'

    'И выполнила царевна условие Яги, да опустила левую руку по запястье в печь, - не был огонь в печи горячим вовсе, а был холодным, как лёд.
    Видит Василиса, что не пламя это, а ручища, что тянутся к ней, да так много их, что и не видно, откуда они поднимаются.'

    $ mc = Character('Василиса', image='vasilisa_second', color='#38977c', kind=nvl, what_prefix = '   {cps=95}"', what_suffix='"')
    
    'Вытащила Василиса руку, да глядит, что точь в точь она костяная, прям как нога у Яги, но присмотрелась царевна - и не костяная вовсе, а серебром блестит, словно изготовлена самым лучшим кузнецом.'

    'Боялась ею пошевелить Василиса, но одолело царевну любопытство. Повела она рукою и поняла, что та словно легче стала, и пальцы двигаются плавно так, как колосья на ветру колышутся.'

    yaga 'Вот теперь любая дверь тебе открыта в царство Кощея, Василиса.' 
    
    yaga 'Но знай одно, - проведешь с собой хоть одно живое существо, найду тебя, царевна, изрублю на мелкие куски, да скормлю зверям, а душу твою скитаться отправлю. Будешь путников несчастных пугать да покоя не найдешь.'

    mc 'Не нарушу указ твой, Яга, а ежели прокрадётся за мной зверь или человек, будь по твоему.'

    mc 'А на этом ли всё, Яга, - перстень, да рука моя? А как же напоить, да накормить, в бани попарить, и спать уложить?'

    'Улыбается царевна, больно быстро Яга её просьбу выполнила, да слишком просто всё. Хоть перстень и дорог ей, но память о сестре она в душе хранит. И не пугают испытания её перед целями, которые она выбрала.'

    'Смеётся в ответ Яга, знает, что каким бы не был исход путешествия царевны, он всё равно повесит её, и сложит она о ней новый сказ, и будет рассказывать добрым молодцам да красным девицам. О сокровищах неслыханных ли будет рассказ, аль о любви единственной, о смерти, или о жизни, - все зависит только от царевны.'

    yaga 'Василиса, душа твоя холодна, как и разум, и хоть жаждешь ты любви, желанию этому не затмить ум твой, не собьешься ты с пути, даже если зверь невиданный попытается сбить тебя с него.'

    'Слушает Василиса Ягу, да знает, что не путь к Кощею главное испытание, ни лес темный, ни зверь хищный, а сам владыка душ и сокровищ.'

    yaga 'А теперь ступай, больше нет у тебя забот в доме моём. Но пред тем как отправиться в путь, ответь мне на вопрос один.'

    mc 'Да что ж не ответить? Что знать ты хочешь, Яга?'

    yaga 'Что на самом деле таится в игле Кощеевой? И хоть нет тут ответа верного, что тебе говорит душа твоя, Василиса?'

    menu:
        '1. Слыхала я, что не жизнь его там спрятана, а память о жизни прошлой, о любви ненайденной, да о судьбах погубленных. Говорят, что не помнит Кощей, что по силе не уступал он богам всевластным, но за лукавство и вероломный характер его был он сослан в царство подземное. С того дня лишь сокровища непомерные - его радость.':
            $ first_choice = 1

            mc 'Слыхала я, что не жизнь его там спрятана, а память о жизни прошлой, о любви ненайденной, да о судьбах погубленных. Говорят, что не помнит Кощей, что по силе не уступал он богам всевластным, но за лукавство и вероломный характер его был он сослан в царство подземное. С того дня лишь сокровища непомерные - его радость.'


        '2. Правду говорят, что в игле жизнь Кощеева, да сломав её - не убить владыку царства мёртвого. Вышита иглой этой судьба его, но сломав её, не соткать судьбы новой.':
            $ first_choice = 2

            mc 'Правду говорят, что в игле жизнь Кощеева, да сломав её - не убить владыку царства мёртвого. Вышита иглой этой судьба его, но сломав её, не соткать судьбы новой.'

        '3. Ходит среди народа молва, что не только жизнь спрятана в игле Кощея, от того так хитроумно запрятана она. Говорят, что игла укажет путь к настоящему его сокровищу.':
            $ first_choice = 3

            mc 'Ходит среди народа молва, что не только жизнь спрятана в игле Кощея, от того так хитроумно запрятана она. Говорят, что игла укажет путь к настоящему его сокровищу.'

    yaga 'Есть правда в ответе твоем, царевна. А теперь отправляйся в путь. Воротишься ты или нет - покажет только время.'

    mc 'Прощай, Яга, и знай, ежели вернусь, то выполню наше соглашение.'

    'Вышла Василиса в сени, прошла снова чрез травы душистые, отворила дверь избы левой рукой да шагнула за порог.'

    jump act_2


label act_2:
    hide hut
    show forest behind frame

    'Вышла царевна, из избы Яги, спустилась по ступеням обратно на поляну, да подняла сапоги свои у дома оставленные, узором вышитые. Надела Василиса их и пошла куда ей сердце велит.'

    'Ни длинен, ни короток путь царевны был, да всё слышит она, как кто-то с ветки на ветку перелетает, да следит за ней.'

    'И обернулась дева черным вороном, взлетела к ветвям, около которых шум слышался. Схватила она птицу, аль зверя, да устремилась к земле.'

    'Ударились они оземь, обратно Василиса облик человеческий приняла, да прижала сапогом гостя непрошенного.'

    'Глядит Василиса, а это филин желтоглазый.'

    mc 'Слышу я, что следишь за мной с первых шагов по лесу мёртвому. Кто послал? Яга мне ль не доверяет, или страж Кощеев ты?'

    'Вырывается филин лесной, крыльями бьёт, да все тщетно, крепко прижала Василиса его к земле.'

    'Да не долго сопротивлялся филин, - знает он, что ежели вырвется, не ускользнуть ему от зоркого взгляда царевны.'

    'Обернулся филин, - и предстал перед Василисой юношей чернобровым, златоглазым, всем складен и ладен он, да видит царевна, обмануть пытается её, под лик человеческий маскируется.'

    'Взмахнула царевна левой рукой пред молодцем, - чары его развеяла.'

    mc 'Да ты лихо!'

    'А лихо то непростое, краше остальных. Лишь хвост как у черта выдаёт его, да когти на руках звериные.'

    'И закричало лихо, голосом нечеловеческим, завыло жалобно так, и говорит царевне.'

    likho 'НЕ ЛИХО! НЕ ЛИХО! ЧЁРТ Я! ВЕДЬМА ТЫ БЕЗСОРОМНАЯ!'

    likho 'ПОГЛЯДИ НА МЕНЯ, НА ХВОСТ МОЙ, ДА КОГТИ ОСТРЫЕ!'

    'Стоит Василиса, на лихо глядит, да весело ей от того, что решил он, будто обмануть её может.'

    'Схватила она его за ухо да как хвост на кулак намотала. Ещё пуще воет лихо, вырваться пытается.'

    mc 'Глянь на себя, уши то твои прям человеческие, а хвост свой видел? Кисточку на хвосте кто украл? Один в один крысиный хвост!'

    'Хохочет царевна, да всё равно знать ей надо, зачем следит за ней чудо лесное. Вопросов пока не задаёт, ждёт, когда лихо само себя выдаст. Отпустила Василиса чудище, да наблюдает, что дальше будет.'

    likho 'НУ И НЕ ВЕРЬ МНЕ, ВЕДЬМА!'

    likho 'ЗНАЛ БЫ, НЕ СЛЕДИЛ ЗА ТОБОЙ!'

    likho 'ВОТ ЗАПЛУТАЕШЬ В ЛЕСУ, НЕ НАЙТИ ТЕБЕ ДОРОГИ К КОЩЕЮ!'

    mc 'Так полагается, Кощею ты служишь?'

    likho 'БОГИ С ТОБОЙ!'
    
    likho 'Живу в лесу этом сколько себя помню, да остерегаюсь Кощея. Выходит он по ночам, обходит владенья свои, а каждую живую душу, что в лесу его заблудилась, к себе уводит.'
    
    likho 'Маманюшка как оставила меня тут, так и живу, а коль увидит Кощей меня, то не видать мне света белого.'

    mc 'И Ягу страшишься?'

    likho 'Яга мне родней мамани!'

    likho 'Помнится поймала она меня ночью пред летним солнцестоянием, прям как ты за хвост схватила, когда я у избы её проходил, да вовнутрь затащила!'
    
    likho 'Всё чёртом меня называла, пока в бани отмывала, после накормила, да обратно отправила. Но перед тем как отпустить, велела мне, чтобы присматривал я за всеми, кто к Кощею идет, чтобы не заплутали.'
    
    likho 'Говорит мне Яга, - {size=+3}{cps=80}"Слушай, Балдей, следи за ними внимательно, да не тревожь меня больше почём зря."{/cps}{/size}'

    'И ясно стало Василисе, с чего лихо чёртом себя возомнило. Яга его Балдеем назвала, да тот не догадался, что подшутила ведьма над ним, а в лес обратно отправила, чтобы под ногами не мешал.'

    mc 'Ну раз Яга сказала, что не лихо ты, а чёрт, спорить с ней не смею.'

    likho 'А НЕ ВЕРИЛА МНЕ ТЫ!'

    likho 'Вроде ведьма прям как Яга, вижу рука твоя, точь в точь костяная, а чёрта от лешего отличить не можешь!'

    'Крутанулся Балдей вокруг своей оси, в ладоши хлопнул, ногой топнул, и глядит Василиса, - глаза лихо золотом светятся.'

    likho 'Так и быть, ведьма, зла на тебя держать не стану, провожу!'

    'И даже не злилась на слова его Василиса, только жаль ей стало лихо, что за каждым встречным следит, да помочь пытается. И не знает истинной природы своей.'

    'И вот идут они вместе сквозь лесную чащу, и сколько путь их продолжается, столько не утихает лихо.'

    likho '...а однажды ночью, слышу, крадется кто-то. Смотрю - да это ящерка! ТЫ БЫ ЕЁ ВИДЕЛА! Я уж думал схватить её, а ящерка эта вся ярче изумрудов, да золотом переливается, и корона крошечная на голове её! И пока рассматривал, она возьми и ускользни под камень.'

    likho '...ой, а помнится мне, однажды прятался я в еловых ветвях от Кощея, так ко мне сначала один полоз под рубаху заполз, а за ним целая тьма. Так я с ними всю ночь и пролежал. На утро гляжу - на подстилке лиственной лежит колечко золотое. А оказалось это не простое колечко, - волшебное! Могу теперь я в любого зверя превращаться, за всеми следить незамеченным.'
    
    likho '...хоть и велела Яга мне к ней не ходить больше, я всё равно навещаю её, а  каждое зимнее солнцестояние на ночь меня оставляет, хоть и ругает, да всё равно на обратную дорогу то змеек даст, то пирогов. Так и живу!'

    'Так и добрались царица и лихо да входа в грот, что ведет в чертог Кощея.'

    likho 'Тут тебя и покину я. Прощай! Ой, только совсем запамятовал я спросить. Как же звать тебя, ведьма?'

    mc 'Василиса я, - царевна королевств вороньих да змеиных.'

    'Побледнело лихо, дрожит как лист осиновый, только к концу пути узнал, кого провожал. Ежели обиделась царевна, то не сносить ему головы. Думает уже обернуться в зверя какого, да видит Балдей, смотрит Василиса на него глазами заботливыми.'

    mc 'Не страшись меня. Ступай обратно, да Ягу навести. Думается мне, всё же скучает она по тебе.'
    
    mc 'А теперь прощай!'

    likho 'Прощай, Васила. Ещё свидимся.'

    jump act_3


label act_3:
    hide forest
    show cave behind frame

    'Чрез грот ступает царевна, и хоть путь неведом ей, знает каждый поворот, - сердце её по верному пути ведет. Под сапогами песок и камни хрустят, то тут, то там ящерицы из под камней убегают прочь.' 
   
    'И чем дальше идет Василиса, тем сильнее холод лютый окутывает пещеру, да тени в сумерках по стенам пляшут, а вдали где-то ветер завывает, прям стонет кто-то.'

    'И тут сквозь ветер слышит голос Василиса, звонкий такой, прям колокольчики на ветру звенят. Совсем рядом звучит, да не видно никого. Остановилась она, к клинку верному тянется, да прислушивается.'

    snake 'То-то же духи одичавшие смолкли - сердце чёрное ступило на эти земли. Сердце цареубийцы, узурпатора и старой девы.'

    'Взглянула Василиса на землю - змея у самых сапог вьётся, яд по клыкам стекает да в иссохшей траве теряется - и сердце её замерло.'

    snake 'Извела со свету ты царя нашего - Владыку змеиного, захватила земли его, но неужто всё мало - решила ты и Повелителя царства Подземного сгубить?'

    'Чувствует Василиса как ненависть вокруг змеи тенью вьётся, да надежда в маленьких глазках блещет - месть в них более личного толка скрывается, пуще, чем на Царевну, обиду та держит на Кощея.'

    menu:
        '1. Не был князь твой мне ровней, да всё же не своими руками погубила его, - сам себя в могилу свёл царевич ваш. Другое дело - тот, кто нынче зовётся Владыкой царства подземного и Бессмертным.':
            jump v1

        '2. Хоть сердце моё и тьмой полно, но вижу в твоём - обида затаилась, мести жаждешь князю владений подземных. Аль убить меня намерена, змея, то знай, я шанс на отмщение твой последний!':
            jump v2

        '3. Вовек не знать тебе ответа, змея!':
            jump v3




label v1:
    mc 'Не был князь твой мне ровней, да всё же не своими руками погубила его, - сам себя в могилу свёл царевич ваш. Другое дело - тот, кто нынче зовётся Владыкой царства подземного и Бессмертным.'

    'От гнева кольцами извивается змея, да в миг кинулась к руке Василисы, прям к той, что около клинка была.'

    snake 'Значит и твоей судьбою в могиле быть!'

    'Потемнело в глазах Василисы, ноги подкосились - коснулась щека земли холодной, и слышит вдали голос чей-то, да слов разобрать не может, а тьма всё сильнее её окутывает.'

    hide cave
    show lair behind frame

    'И очнулась Василиса в обители Кощея, села на перине гусиной да чувствует, как жар её со всех сторон окутал. Смотрит царевна, а тепло льется от печи, что синим пламенем объята.' 
    
    'И стоят в печи той кельмы, жидкого золота полные. Осматривается Василиса, - стены все золотыми гобеленами увешаны, сюжеты на них от начала времён тянутся к времени настоящему. А по полу нить золотая стелется.'

    'Чуть не убита была царевна укусом змеи, и хоть знает, кого благодарить за спасение своё, да не видно нигде Кощея.'

    'Медленно на ноги поднимается Василиса да покои царские обходит, рисунки чудные разглядывает. И больше всех её один привлекает - тот, на котором двенадцать богов юношу заковывают, а на следующем его в этих самых цепях орёл исполинский в ущелье тёмное сбрасывает.'

    koschey 'Не стоит доверять им'

    'За спиной голос слышится. Оглядывается Василиса, а перед ней юноша стоит.'

    'Одеяние его золотом расшито, грозди бузины на кафтане том. В глазах огонь горит, а волосы медными волнами ниспадают, такой же медью усы да борода его светятся. На руках когти острые златом блестят.'

    koschey 'Знай, Василиса, жизнь твоя лишь на время в руках у тебя'

    koschey 'Не властен я над судьбою смертных, да не в моих руках жизнь твоя - лишь на время отсрочить неизбежное могу.'

    koschey 'Долгий путь ты проделала, царевна, и ежели по своей воле пришла ко мне да с намерениями добрыми, в царствие моём можешь жизнь новую обрести.'

    koschey 'Да и любопытно мне, что стольких девиц красных погубил я, а ты сама ко мне наведалась да стольким по пути пожертвовала.'

    koschey 'Что же именно привело тебя, Василиса?'

    'Со словами этими мимо царевны проходит Кощей, к печи самой. Да в кельму когти свои острые опускает. Видит Василиса, как золото жидкое в руках Кощея в нить превращается.'

    'На трон садится князь подземный, с любопытством на царевну глядит, да ответа ждёт.'

    mc 'Ходит в народе молва, что ведьма я с сердцем темнее ночи. Называют грозной правительницей царства вороньего да той, по чьему велению убит был змей великий. А те, кто смелее других, говорят, что царством своим в одиночку правлю, от того, что ради сокровищ сгубила всех женихов своих.'

    mc 'Правды много в словах этих, да только не ведает народ о жертвах, что отданы были да от которых сердце моё таким тёмным стало. Сколько горя выдержала я, и какой жертвой власть мне дана.'

    mc 'К тебе пришла с надеждой последней, о любви надежда та, - о том, чего сердце моё больше всего жаждет. И пусть молвят, что только за сокровищами в царство твоё спустилась я, знай, - то лишь ложь.'

    'И чувствует царевна, что всё дурнее становится ей. Сердце тьмой от мести змеиной окутано.'

    koschey 'Не в моих силах любовь тебе в столь короткую встречу подарить, да есть предложение, что сохранит жизнь твою.'

    koschey 'Знают все, что жизнь моя заточена в игле заколдованной, в той, что однажды переломил Иван-царевич, тем самым одолев меня.'

    koschey 'Однако пред тобой сижу я, Василиса, да только правда в том, что переломил он жизнь мою земную, и отныне заточен я в царстве своём. Покуда нахожусь в нем, вынужден ткать я судьбы жизни подземной.'

    koschey 'Ежели согласишься, царевна, знай, не править тебе больше царством своим, но все богатства мои в твоём распоряжении будут.'

    'Пошатнулась царевна, и сама не знает, от предложения столь хитрого, аль от отсутствия сил своих.'

    mc 'И не покинуть мне царства твоего, Кощей? Век свой без власти да одиночестве провести?'

    koschey 'Как знать, царевна, может и обретешь любовь со мной.'

    koschey 'Известно тебе, что многих красных девиц похищал я. Вот только ни одна не согласилась рядом со мной править, сердце своё доброе тьме предложить.'

    koschey 'А тобой, Василиса не меньше богатырей добрых загублено, тех, что сгинули в испытаниях хитрых.'

    koschey 'Согласишься ли ты, царевна, ткать судьбу загробную за меня?'

    koschey 'И в день моего отмщения вернусь в царство тёмное, к тебе вернусь, Василиса.'

    koschey 'Так согласна ль ты?'

    menu: 
        '1. Отпрянула Василиса, - кажется ей, что ложь в словах Кощеевых змеёй вьётся':
            jump v1_1

        '2. Медленно к Кощею подошла Василиса. Столь долгий путь она проделала и чувствует, что не просто так испытывает её судьба.':
            jump v1_2


label v1_1:
    'Отпрянула Василиса, - кажется ей, что ложь в словах Кощеевых змеёй вьётся'

    mc 'Не бывать этому, Кощей, не за этим столь долгий путь преодолела я.'

    mc 'Коль суждено мне с жизнью расстаться - так тому и быть.'

    'С тоской Кощей ей отвечает.'

    koschey 'Горестно мне от выбора твоего, но воля твоя, Василиса, да не мне её решать.'

    'Пред Кощеем стоит Василиса, с огнем глазах на князя тёмного смотрит. А сил всё меньше, -  на минуты счёт жизнь её идёт.'

    'И готова уж оземь удариться царевна, как вдалеке слышит шаги, столь знакомые да голос, что на шелест листвы похож.'

    yaga 'Коль на его условия не согласная, Василиса, да о моем не забывай.'

    'И голос её весельем полон.'

    'Топнула Яга ногой костлявой, звон по залу раздался, и завился вдруг вокруг неё огонь волшебный.'

    'А Кощей молча сидит на троне, лишь наблюдает смиренно.'

    yaga 'Время есть у тебя еще судьбу свою выбрать, Василиса, и ежели в круг огня зайдешь, то отныне быть тебе помощницей моей, - той, кто в летний зной в мир людей является.'

    'Обернулась Василиса черным вороном, да в круг огня скользнула. Поглотил он царевну, и только дым черный по залу стелется.'

    koschey 'Вижу, Яга, шанс свой упускать не намерена?'

    yaga 'Не моя вина, что видимо не по нраву ей предложение твоё, а мне помощница верная лишней не будет. Хотя жаль мне тебя, Кощей, славное царство бы было у тебя с Василисой, да видимо сердце её не готово довериться тебе.'

    yaga 'Да как знать, может встретитесь ещё с ней.'

    'Из огня вышла Василиса, - прежней, да на Кощея смотрит царским взором своим, а на устах улыбка едва заметна.'

    mc 'Прощай, князь подземный.'

    'Исчезла Василиса да Яга, и сидит Кощей в зале златом, прядёт золотую нить, да ею ткёт судьбу свою, и так день за днём, год за годом да век за веком.'

    jump end_credits


label v1_2:
    'Медленно к Кощею подошла Василиса. Столь долгий путь она проделала и чувствует, что не просто так испытывает её судьба.'

    mc 'Согласная я, Кощей, да чувствую, что медлить мне нельзя.'

    koschey 'Возьми в руку, Василиса, иглу, да садись за полотно чистое, и если справишься с тем, что покажет тебе она, то быть тебе на веки рядом со мной.'

    'И протягивает в руку ей Кощей иглу острую, да игла та не из золота, как всё вокруг, а серебром светится.'

    'Взяла Василиса иглу, да за полотно чёрное села, и хоть сил всё меньше, но знает, не от этого теперь жизнь её зависит.'

    'Да только первый стяжек сделала царевна, как окутал туман разум её, и предстали пред ней видения. О прошлом да о будущем они, о мире земном да загробном.' 
    
    'В одно мгновение видит она, что в мире ином прикован Кощей, на дне подземелья глубокого, а рядом исполинских размеров змей вьётся да ядом раскаленным тело князя истязает.' 
    
    'В другой момент вот он уже на коне в сражении славном, но пал от рук бога огнегривого, и вьются над ним вороны в час темный. И чем дальше смотрит Василиса, тем сильнее разум её в тьму погружается, всё больше и больше видений перед ней предстают.'

    'Но в какой-то момент слышит она голос женский, тихо так колыбельную напевает он, - о начале времен песнь та да о сражениях великих, о несметных сокровищах да любви, что неизвестна богам.'

    'И больше не чувствует царевна боли и тьмы от укуса змеиного. Гладко иголка идёт по канве, и слышно только в тишине зала, как напевает она песню, что миг назад привиделась ей.'

    jump end_credits


label v2:
    mc 'Хоть сердце моё и тьмой полно, но вижу в твоём - обида затаилась, мести жаждешь князю владений подземных. Аль убить меня намерена, змея, то знай, я шанс на отмщение твой последний!'

    'И тут задумалась змея, зашипела, но отступила от царевны.'

    snake 'Думается мне, царевна, всей правды ты, конечно, не скажешь в царстве Кощеевом.'

    snake 'Но так и быть, доверюсь я тебе, не упущу своего шанса. Но случись это ложь, желаю тебе самой погибнуть от руки Владыки.'

    'С этими словами под камень ускользнула змея, словно и не было её у ног царевны мгновение назад.'

    'И дальше в путь отправилась Василиса, во тьме пещеры идет. И где-то вдали свет мягкий в тумане видится. В мгле тумана ступает царевна, и вот уж виднеется пред ней мост каменный.'

    hide cave
    show lair behind frame

    'Столбы моста камнями драгоценными усыпаны да на каждом из них свисают сотнями черепа людские, а из глазниц свет так и льётся. Под ногами мрамор, как ночь чёрный, в нём то тут, то там, звёзды золотые блестят.'

    'Проходит дальше Василиса, смело ступает через ряд столбов, и вот перед ней двери зала великолепного сами отворяются, -  сокровищ полон он. Смотрит Василиса, а жемчуга через край сундуков сыпятся, ткани парчовые горами сложены да монеты золотые прям под ногами звенят, гранитных плит под ними не видать. И чем дальше идет царевна, - чувствует, всё теплее становится, холод могильный, что в пещере так ощущался, не беспокоит боле.'

    'На следующих вратах, тех что до самого потолка возвышаются, рельеф выточен. И на рельефе этом сюжет мифический, - всё пространство черепами усыпано, а в центре на двенадцати цепях юноша красный к дубу прикован.'

    'Отворяет Василиса те врата, а зал этот с потолка до пола черным мрамором выстелен. Ни души в зале этом, и только цепи по стенам висят. Бесшумно по плитам гранитным ступает царевна к вратам следующим, и видно только, как свет золотой из-под них льётся.'

    'Сами собой врата эти распахиваются, и окутал жар Василису, что тянулся из зала следующего. Вслед за жаром слышит голос царевна, что по залу темному гулко разносится.'

    koschey 'Весть ко мне эхом донеслась, что ведьма в лесу моём бродит, да беспокоит обитателей царства моего.'

    koschey 'Хочу поглядеть на тебя, Василиса, да узнать, что привело тебя ко мне.'

    koschey 'И ступает Василиса в обитель Кощея да глядит, что жар льется от печи, синем пламенем объятой, и стоят в печи той кельмы, жидкого злата полные. Стены все золотыми гобеленами увешаны, сюжеты на них от начала времён тянутся к времени настоящему. А по полу нить золотая стелется.'

    'На троне сидит Кощей. Волосы его медными волнами ниспадают, такой же медью - усами острыми да бородой роскошной - лицо его обрамлено, а в глазах огонь горит. Но холодно так на Василису он смотрит. Без его приглашения в царство подземное явилась Василиса.'

    koschey 'Вот и добралась до меня ты, красна девица. Скрывать не стану, любопытно мне стало услышать из уст твоих историю - кто ты и зачем свою душу губишь сделками ведьминскими, чтобы со мною увидиться, когда любая на твоём месте перекрестилась бы и молила бога отвести от злых когтей моих.'

    'И правда на руках его когти златом блестят, и замечает царевна, что нить в руках его. Медленно плетет нить ту Кощей.'

    'И ласково так отвечает ему царевна.'

    mc 'Владыка Бессмертный, именем и судьбою моими будут Василиса, - Повелительница царства воронов. Да только не мило мне царство Наземное без спутника равного.'

    mc 'И хоть душа моя чужой кровью запятнана, лишь от того это что не готова продать её за бесценок случайному. Коли свет такой жертвы требует, - пусть тени её окутают по разумению моему.'

    mc 'Так что не страшат меня ни грех, ни яд змеиный, ни когти твои золотые, Кощей.'

    koschey 'Известно мне, что твоими указами много женихов похоронено. Все они встретили свой конец здесь душами заблудшими - алчностью, вызванной твоим образом лунным, ослеплённые.'

    koschey 'Настолько хотели обладать тобою, что шли на любые испытания - то за папоротника цветком в болото гиблое, то в соседнее княжество к Змею Великому, твоей сестры государю и мужу.'

    'Поднимается с трона своего Кощей, выпуская из рук нить золотую, неспеша к Василисе подходит. Эхом шаги его по залу разносятся.'

    koschey 'Вижу, что Яга с тобой негласный договор заключила, да знай, Василиса, что ещё никто царства моего не покидал. Ни добры молодцы, что пришли силой помериться, ни красны девицы, что отказались свататься со мной. Быть теперь им до самой смерти ящерицами, змеями да поганью лесной.'

    koschey 'Но как и всех, спрошу тебя, Василиса, готова ли ты побороться за облик человеческий, аль вовсе за жизнь свою? А может и сможешь доказать, что достойна ты находиться в обителе моей.'

    koschey 'Если воля твоя сильна, то испытание моё тебе будет не тяжелее пути, что ты прежде преодолела. Покажет оно, насколько ты духом сильна, чтобы в нём не погибнуть, да найти своё место у трона Царства Подземного, а не среди душ женихов своих и травы иссохшей?'

    'С этими словами протянул Кощей иглу, через которую была продета нить золотая.'

    koschey 'Ежели не согласишься, не быть тебе более царицей.'

    mc 'Согласна я на испытание твоё, Кощей.'

    koschey 'Возьми в руку, Василиса, иглу, да садись за полотно чистое, и если справишься с тем, что покажет тебе она, то быть тебе на веки рядом со мной.'

    menu:
        'И знает Василиса, что хоть и не простое это испытание, да сил ей хватит справится с ним.':
            jump v2_1

        'Знает царевна, что кому как не ей пройти испытание Кощеево.':
            jump v2_2


label v2_1:
    'И знает Василиса, что хоть и не простое это испытание, да сил ей хватит справится с ним.'

    'И кладёт в руку ей Кощей иглу острую, да игла та не из золота, как всё вокруг, а серебром светится.'

    'Взяла Василиса иглу да за полотно чёрное села, и чувствует, как от иглы этой теперь её жизнь зависит.'

    'Как только первый стяжек сделала царевна, - окутал туман разум её, и предстали перед ней видения. О прошлом да о будущем они, о мире земном да загробном.' 
    
    'В одно мгновение видит она, что в мире ином прикован Кощей, на дне подземелья глубокого, а рядом исполинских размеров змей вьётся да ядом раскаленным тело князя истязает.' 
    
    'В другой момент вот он уже на коне в сражении славном, но пал от рук бога огнегривого, и вьются над ним вороны в час темный. И чем дальше смотрит царевна, тем сильнее разум её в тьму погружается, всё больше и больше видений перед ней предстают.'

    'Но в какой-то момент слышит она голос женский, тихо так колыбельную напевает он, - о начале времен песнь та да о сражениях великих, о несметных сокровищах да любви, что неизвестна богам.'

    'Гладко иголка идёт по канве, и слышно только в тишине зала, как запевает она песню, что миг назад привиделась ей. И знает царевна, что хоть любовь истинную свою не сразу суждено ей обрести, да наконец нашла она главное сокровище сердца своего тёмного, - покой от горести о людях да времени ушедшем.'

    jump end_credits


label v2_2:
    'Знает царевна, что кому как не ей пройти испытание Кощеево.'

    'И протягивает в руку ей Кощей иглу острую, да игла та не из золота, как всё вокруг, а серебром светится.'

    'Взяла Василиса иглу да за полотно чёрное села, и чувствует, как от иглы этой теперь её жизнь зависит.'

    'Как только первый стяжек сделала царевна, - окутал туман разум её, и предстали перед ней видения. О прошлом да о будущем они, о мире земном да загробном.' 
    
    'В одно мгновение видит она, что в мире ином прикован Кощей, на дне подземелья глубокого, а рядом исполинских размеров змей вьётся да ядом раскаленным тело князя истязает.' 
    
    'В другой момент вот он уже на коне в сражении славном, но пал от рук бога огнегривого, и вьются над ним вороны в час темный. И чем дальше смотрит царевна, тем сильнее разум её в тьму погружается, всё больше и больше видений перед ней предстают.'

    'И вот самое яркое перед ней, то, что видела прежде она, на вратах, что в зал темный вели. Только теперь не Кощей в цепи железные закован, а она сама. В цепи, что любого сил магических лишают. И никак не выбраться ей из цепей этих, - не обернуться черным вороном.' 
    
    'В следующий миг чувствует, что прикована она в темнице темной, куда не попадает свет солнечный, и только ветер вдали завывает. И проходит так день, другой, а за ним недели да года тянутся. И так век за веком во тьме царевна заточена.'

    'Встал с трона Кощей, забрал из рук Василисы иглу, да обратил царевну в черного ворона. Взлетела она в облике птичьем к изголовью трона и села на него - вовек там ей быть и суждено. Не заполучить ей больше ни любви, ни сокровищ Кощеевых.'

    jump end_credits


label v3:
    mc 'Вовек не знать тебе ответа, змея!'

    'В тот же миг кинулась на Василису змея, да вонзила клыки свои острые.'

    'Долог ли, короток ли век царевны, да не видеть ей больше света белого, не править царством своим славным, и не найти любви вовек.'

    'Змея со свету белого сжила красавицу, властительницу царств вороньих.'

    jump end_credits


label end_credits:
    $ quick_menu = False
    # hide scene background_image
    scene black

    show screen credits with dissolve
    pause
